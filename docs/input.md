# テストケースの指定

テストケースの内容は以下のいずれかの形式で指定されます。

```
[変数の宣言]
---
[出力の形式]
```

または

```
[変数の宣言]
+++
[値の書き換え]
---
[出力の形式]
```

`%%` からその行の終わりまではコメントになります。

## 変数の宣言

- 整数
- 浮動小数点数
- 文字列
- 整数の配列
- 浮動小数点数の配列
- 文字列の配列
- 整数の行列
- 浮動小数点数の行列
- グラフ

を変数にできます。

### 整数

```
int [変数名] [値の範囲]
```

と宣言します。例えば 1 以上 100 以下の整数 `N` は

```
int N [1, 100]
```

とします。

変数名には英小文字(a, b, ..., z), 英大文字(A, B, ..., Z), 数字(0, 1, ..., 9), アンダースコア(_)を使うことができます。ただしアンダースコアから始まる変数名は使えません(`_i`, `_j` などの名前の変数は後述のように他の意味を持ちます)。

また、Python の予約語や `sum`, `min`, `max` といった名前も変数名として使えないようにしています。使いたい場合は先頭を大文字にするなどして完全一致を回避してください。

範囲の記法は数学で用いられる区間の記法と同じです。

```
[1, 100]  %% 1 以上 100 以下
[1, 100)  %% 1 以上 100 未満
(1, 100]  %% 1 より大きく 100 以下
(1, 100)  %% 1 より大きく 100 より小さい
```

値の範囲には定数だけでなく他の整数または浮動小数点数の変数を用いることができます。また、数式を書くこともできます。変数を宣言する順番は任意に変えることができ、値の範囲にまだ宣言されていない変数を用いることもできます。

```
int a [-100, 100]

%% N は a 以上 b 以下
int N [a, b]

%% b は a*(a-1) 以上 200000 以下
int b [a * (a-1), 200000]
```

### 浮動小数点数

整数の宣言の `int` を `float<[小数点以下の桁数]>` に変えると出力する小数点以下の桁数を指定して浮動小数点数を宣言できます。

```
%% r は 0 以上 1 未満で、小数第 2 位まで出力される浮動小数点数
%% すなわち 0.00 <= r <= 0.99
float<2> r [0, 1)
```

### 文字列

```
str<[文字種]> [変数名] [長さの範囲] [属性]
```

と宣言します。

#### 文字種

文字列に含まれる文字の種類です。

```
%% a, b からなる文字列(abbabaaab など)
str<ab>

%% x, y, z, !, ? からなる文字列
str<xyz!?>
```

`-` を用いて文字の範囲を指定することもできます。

```
%% a から z (英小文字)からなる文字列
str<a-z>

%% A から Z と a から z からなる文字列
str<A-Za-z>

%% 0 から 9 と . と ? からなる文字列
str<0-9.?>
```

`-`, `\`, `>` を含めたい場合は、`\` でエスケープします。

```
%% < と > からなる文字列 (> は \> と書く)
str<<\>>

%% - と \ からなる文字列 (-, \ はそれぞれ \-, \\ と書く)
str<\-\\>
```

#### 変数名・長さの範囲

整数・浮動小数点数の宣言と同様に指定します。

```
%% a から z からなる長さ 1 以上 10 以下の文字列
str<a-z> S [1, 10]

%% . と # からなる長さ M 以上 N / 2 未満の文字列
str<.#> T [M, N / 2)
```

#### 属性

空文字列または `distinct` を指定します。`distinct` を指定した場合、文字列の中に同じ文字が現れないようになります。

```
%% S は abc,acb,bac,aca,cab,cba のいずれか
str<abc> S [3, 3] distinct

%% T はその限りではない (aab,ccc なども生成される可能性が有る)
str<abc> T [3, 3]
```

### 配列

```
%% 横に出力される配列
row<[型], [長さ]> [変数名] [値の範囲または文字列の長さ] [属性]

%% 縦に出力される配列
col<[型], [長さ]> [変数名] [値の範囲または文字列の長さ] [属性]
```

「横に出力される配列」は、例えば以下のようなものです。

```
1 2 3 4 5
```

「縦に出力される配列」は、例えば以下のようなものです。

```
9
5
1
2
```

#### 型

`int`, `float<[小数点以下の桁数]>`, `str<[文字種]>` のいずれかを指定できます。

#### 長さ・変数名

配列の要素数と変数名を指定します。

```
%% 長さ N の、横に出力される文字列の配列 A
row<str<a-z>, N> A

%% 長さ 10 の、縦に出力される整数の配列 B
col<int, 10> B
```

#### 値の範囲または文字列の長さ

整数または浮動小数点の配列では、要素の値の範囲を指定します。

```
%% 要素数 10 の、横に出力される 1 以上 400 以下の整数の配列 A
row<int, 10> A [1, 400]
```

値の範囲に同じ長さの他の配列を指定した場合、その配列の値が要素毎に代入されます。

```
col<float<3>, 10> A [1, 100]

%% 要素ごとに比較して各要素は A より大きい値
col<int, 10> B (A, 100]

%% 要素ごとに比較して各要素は A 以下の値
col<int, 10> C [1, A]
```

`_i` を値の範囲の指定に含めると、各要素の添字 (0-indexed) が代入されます。

```
%% i 番目の要素が i 以上 2i+10 以下
row<int, 10> A [_i, _i * 2 + 10]
```

文字列の配列では、要素となる文字列の長さの範囲を指定します。

```
%% 要素数 N の、縦に出力される長さ 10 以上 20 以下の文字列の配列 B
col<str<0-9?>, N> B [10, 20]
```

ここで、文字列の配列の各要素の長さは一般に同じにならないことに注意してください。

```
row<str<ab>, 5> C [1, 5]
```

```
C の例
abba a bb ba aaba
```

要素の長さの下限と上限を一致させると全ての要素の長さを揃えられます。

```
int H [1, 10]
int W [1, 10]
col<str<.#>, H> S [W, W]
```

```
S の例
...##..
###.#.#
...##..
###.###
.#..##.
#.#.##.
###...#
#.#.##.
```

#### 属性

属性には

- `unique`
- `inc`
- `dec`
- `distinct`

をそれぞれ 0 個または 1 個含む空白区切りの文字列を指定できます。

ただし、`inc` と `dec` は整数または浮動小数点数の配列に対してのみ、`distinct` は文字列の配列に対してのみ効果を持ちます。

##### `unique`

指定すると、配列の中で同じ要素が複数現れないようになります。

##### `inc`

整数または浮動小数点数の配列に対して指定すると、配列の要素が広義単調増加になります。`unique` と組み合わせると狭義単調増加にすることができます。

##### `dec`

整数または浮動小数点数の配列に対して指定すると、配列の要素が広義単調減少になります。`unique` と組み合わせると狭義単調減少にすることができます。

`inc` と `dec` は同時に指定できません。

##### `distinct`

文字列の配列に対して指定すると、要素となる各文字列の中に同じ文字が出現しないようになります。

```
%% 長さ N の、同じ要素を持たない 1 以上 N 以下の整数の配列 (長さ N の順列)
row<int, N> A [1, N] unique

%% 長さ N の狭義単調増加な数列
row<float<10>, N> B [1, 9.2] inc unique
```

### 整数の行列

整数の行列は以下のように宣言します。

```
mat<int, [行数], [列数]> [変数名] [値の範囲] [属性]
```

行数・列数・変数名・値の範囲の指定はこれまでと同様です。

```
%% 各要素が 1 以上 10 以下の H×W 行列
mat<int, H, W> M [1, 10]

%% 各要素が A 以上 A+B 未満の 10×10 行列
mat<int, 10, 10> N [A, A + B)
```

ただし、値の範囲の指定に `_i`, `_j` を含めると各要素が上から何行目であるか・左から何列目であるかを表す添字 (0-indexed) が代入されます。

```
%% (i, j) 成分が (i*5 + j) の 5×5 行列
mat<int, 5, 5> M [_i * 5 + _j, _i * 5 + _j]
```

```
M として生成される行列
0 1 2 3 4
5 6 7 8 9
10 11 12 13 14
15 16 17 18 19
20 21 22 23 24
```

#### 属性

属性には空文字列または `unique` が指定できます。`unique` を指定した場合、行列の中に同じ要素が現れなくなります。

### 浮動小数点数の行列

浮動小数点数の行列は以下のように宣言します。

```
mat<float<[小数点以下の桁数]>, [行数], [列数]> [変数名] [値の範囲] [属性]
```

各項目の内容は整数の行列と同様です。

### グラフ

[木](https://ja.wikipedia.org/wiki/%E6%9C%A8_(%E6%95%B0%E5%AD%A6)) は以下のように宣言します。

```
tree<[頂点数]> [変数名] [属性]
```

[スターグラフ](https://ja.wikipedia.org/wiki/%E3%82%B9%E3%82%BF%E3%83%BC_(%E3%82%B0%E3%83%A9%E3%83%95%E7%90%86%E8%AB%96))・[パスグラフ](https://ja.wikipedia.org/wiki/%E3%82%B0%E3%83%A9%E3%83%95_(%E9%9B%A2%E6%95%A3%E6%95%B0%E5%AD%A6)#%E3%83%91%E3%82%B9%E3%82%B0%E3%83%A9%E3%83%95)・[二分木](https://ja.wikipedia.org/wiki/%E4%BA%8C%E5%88%86%E6%9C%A8)がそれぞれ 10% の確率で生成され、70% の確率でランダムな木が生成されます。

頂点数と変数名の指定はこれまでと同様です。

#### 木の属性

属性には

- `directed`
- `[整数]_indexed`

をそれぞれ 0 個または 1 個含む空白区切りの文字列が指定できます。


##### `directed`

指定すると[有向木](https://ja.wikipedia.org/wiki/%E3%82%B0%E3%83%A9%E3%83%95_(%E9%9B%A2%E6%95%A3%E6%95%B0%E5%AD%A6)#%E6%9C%89%E5%90%91%E6%9C%A8)が生成されます。

##### `[整数]_indexed`

指定すると頂点番号が [整数]-indexed な木が生成されます。省略すると頂点番号は 1-indexed となります。

```
%% N 頂点の木
tree<N> T1

%% 2N 頂点の有向木
tree<2 * N> T2 directed

%% 10 頂点で頂点番号が 0-indexed な有向木
tree<10> T3 directed 0_indexed
```

[木](https://ja.wikipedia.org/wiki/%E6%9C%A8_(%E6%95%B0%E5%AD%A6))ではないグラフは以下のように宣言します。

```
graph<[頂点数], [辺数]> [変数名] [属性]
```

頂点数と辺数と変数名の指定はこれまでと同様です。

#### 一般のグラフの属性

属性には

- `directed`
- `connected`
- `acyclic`
- `no_multiple_edge`
- `no_self_loop`
- `simple`
- `[整数]_indexed`

をそれぞれ 0 個または 1 個含む空白区切りの文字列が指定できます。

##### `directed`

指定すると[有向グラフ](https://ja.wikipedia.org/wiki/%E3%82%B0%E3%83%A9%E3%83%95%E7%90%86%E8%AB%96#%E6%9C%89%E5%90%91%E3%82%B0%E3%83%A9%E3%83%95)が生成されます。

##### `connected`

指定すると[連結](https://ja.wikipedia.org/wiki/%E9%80%A3%E7%B5%90%E3%82%B0%E3%83%A9%E3%83%95)なグラフが生成されます。

##### `acyclic`

指定すると[閉路](https://ja.wikipedia.org/wiki/%E9%96%89%E8%B7%AF)を持たないグラフが生成されます。

##### `no_multiple_edge`, `no_self_loop`

それぞれ、指定すると[多重辺・自己ループ](https://ja.wikipedia.org/wiki/%E3%82%B0%E3%83%A9%E3%83%95%E7%90%86%E8%AB%96#.E3.83.AB.E3.83.BC.E3.83.97.E3.81.A8.E5.A4.9A.E9.87.8D.E3.82.B0.E3.83.A9.E3.83.95)を持たないグラフが生成されます。

##### `simple`

指定すると単純グラフが生成されます。この属性を指定することは `no_multiple_edge` と `no_self_loop` をともに指定することと等価です。

##### `[整数]_indexed`

指定すると頂点番号が [整数]-indexed なグラフが生成されます。省略すると頂点番号は 1-indexed となります。

```
%% V 頂点 E 辺のグラフ
%% 注意: 属性を全く指定していないので連結とも閉路を持たないとも単純グラフであるとも限らない
graph<V, E> G1

%% V 頂点 E 辺の閉路を持たない有向グラフ
graph<V, E> G2 acyclic directed

%% 頂点番号が 0-indexed な単純グラフ(多重辺・自己ループが無いグラフ)
graph<V, E> G3 simple 0_indexed
```

縦に出力される配列と組み合わせることで辺に重みがついたグラフ(木を含む)も生成できます(後述)。

## 値の書き換え

テストケースの内容は以下のいずれかの形式で指定されます。(再掲)

```
[変数の宣言]
---
[出力の形式]
```

または

```
[変数の宣言]
+++
[値の書き換え]
---
[出力の形式]
```

下の形式では `+++` と書かれた行より下、`---` と書かれた行より上の範囲に値の書き換えの指定をすることができます。

値の書き換えとは、全ての変数をランダム生成して値を確定させた後に Python スクリプトで変数の内容をいじることです。例えば `.` と `#` からなる `H` 行 `W` 列のマップは

```
int H [50, 100]
int W [50, 100]
col<str<.#>, H> S [W, W]
```

などと宣言することで生成できますが、「最も左上の文字と最も右下の文字が `.` である」などの条件を更に課したい場合

```
int H [50, 100]
int W [50, 100]
col<str<.#>, H> S [W, W]
+++
S[0][0] = '.'      %% 最も左上の文字を . で書き換える
S[H-1][W-1] = '.'  %% 最も右下の文字を . で書き換える
```

とすることで実現できます。

それぞれの変数は Python のリストとして保持されていて、Python のリストには複数の型の値を混在させることもできるので

```
int N [5, 10]
col<float<5>, N> A [0, 20]
+++
for i in range(1, N, 2):
    A[i] = 'Hello'
```

```
A の例
8.40588
Hello
14.88826
Hello
5.17600
Hello
```

のようなこともできます。

値の書き換えは生成した値を後から強制的に上書きする機能であり、例えば長さ `N` の配列 `A` を生成した後に `N` の値を書き換えても `N` の値が変わるだけで `A` の長さは変わらないことに注意してください。

Python で使用できる全ての構文が使用できるわけではありません。例えば、f-string (`f'The value is {value}.'` などの形式の文字列) や triple quotes (`'''` と `"""`) は使用できません。一方で、ファイルを削除するなどの副作用を持つスクリプトを書いた場合にそれが実行されて副作用が発生する可能性があるので注意してください。

## 出力形式

[AtCoder の問題](https://kenkoooo.com/atcoder#/table/)の「入力形式」の欄のように変数名を並べて記載します。

### 整数・浮動小数点数・文字列の出力

3 つの整数 A, B, C をこの順で 1 行に並べたテスト入力を生成するには以下のようにします。

#### 入力

```
int A [1, 10]
int B [1, 10]
int C [1, 10]
--- %% ここから下が出力形式の欄
A B C
```

#### 出力例

```
7 2 4
```

途中に改行を挟むとその部分で改行されます。

#### 入力

```
int A [1, 10]
int B [1, 10]
int C [1, 10]
---
A B
C
```

#### 出力例

```
7 2
4
```

浮動小数点数・文字列の出力も同様です。

#### 入力

```
int a [1, 10]
float<2> b [a, 10]
str<a-z> s [a, a]
---
s b
a
```

#### 出力例

```
dbwjhm 8.99
6
```

### 配列の出力

配列の変数名を書くと配列が出力されます。宣言時に指定された出力の方向 (`row` または `col`) に応じて出力が変わります。

#### 入力

```
row<int, 5> A [1, 1]  %% row は横に出力される
row<int, 5> B [2, 2]
---
A
B
```

#### 出力

```
1 1 1 1 1
2 2 2 2 2
```

#### 入力

```
row<int, 5> A [1, 1]
row<int, 5> B [2, 2]
---
A B
```

#### 出力

```
1 1 1 1 1 2 2 2 2 2
```

#### 入力

```
col<int, 5> A [1, 1]  %% col は縦に出力される
col<int, 5> B [2, 2]
---
A B
```

#### 出力

```
1 2
1 2
1 2
1 2
1 2
```

#### 入力

```
col<int, 5> A [1, 1]
col<int, 5> B [2, 2]
---
A
B
```

#### 出力

```
1
1
1
1
1
2
2
2
2
2
```

### 行列の出力

行列の変数名を書くと行列が出力されます。

#### 入力

```
mat<int, 3, 3> M [0, 1]
---
M
```

#### 出力例

```
0 1 1
0 1 0
1 0 1
```

### グラフの出力

グラフの変数名を書くと辺の両端の頂点対のリストを縦に出力します。有向グラフの場合、左側の頂点から右側の頂点に向かう辺が存在することを示します。

#### 入力

```
tree<10> T
---
T
```

#### 出力例

```
8 10
5 1
2 9
1 3
10 6
2 4
4 7
6 9
8 3
```

#### 入力

```
int V [5, 10]
int E [V-1, V * (V-1) // 2]
graph<V, E> G connected
---
V E
G
```

#### 出力例

```
6 10
3 4
1 4
1 5
1 6
1 2
1 6
1 2
1 6
3 4
3 4
```

辺の数だけ要素を持つ配列をグラフと一緒に縦に出力することで辺に重みがついたグラフを生成することもできます。

#### 入力

```
int V [5, 10]
int E [V-1, V * (V-1) // 2]
graph<V, E> G simple
col<int, E> W [-100, 100]  %% 重みの配列
---
V E
G W
```

#### 出力例

```
10 16
2 1 38
5 10 0
4 1 -63
6 9 9
4 3 -87
8 2 84
6 7 -34
3 9 -92
9 1 -56
4 10 -90
4 8 -3
3 5 -83
9 8 -64
3 10 18
10 9 48
2 10 -75
```
